// Cycle Monitor, Copyright (C) 2015  M.B.Grieve
// This file is part of the Cycle Monitor example application.
//
// Cycle Monitor is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// Cycle Monitor is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with City Monitor.  If not, see <http://www.gnu.org/licenses/>.
//
// Contact: moray.grieve@me.com

package com.jtech.alert;
using com.jtech.config.DBKey;
using com.jtech.config.CreateUpperBoundaryAlert;
using com.jtech.config.CreateLowerBoundaryAlert;
using com.jtech.config.CreateRateThresholdAlert;
using com.jtech.source.StationUpdate;

monitor AlertManager {
	IAlertManager this;
	com.jtech.util.KeyedDataViewHelper dvHelper;
	dictionary < DBKey, sequence<IAlertStrategy> > strategies;

	action onload {
		log "Alert manager loaded" at INFO;

		//register the alert dataview
		dvHelper.register((new Alert).iface());

		//initialise the interface
		this.addAlert := addAlert;
		this.clearAlert := clearAlert;

		//handle create requests
		createUpperBoundary();
		createLowerBoundary();
		createRateThreshold();
		
		//pass updates to the relevant strategies
		StationUpdate stationUpdate;
		on all StationUpdate():stationUpdate {
			DBKey key := DBKey(stationUpdate.city, stationUpdate.id);
			if (strategies.hasKey(key)) then {
				IAlertStrategy strategy; for strategy in strategies[key] {
					strategy.check(stationUpdate);
				}
			}
		}
	}

	action addAlert(IAlertStrategy strategy, StationUpdate update, string message) {
		strategy.pprint(message+", adding alert to strategy");
		dvHelper.add((Alert(update.city, update.id, update.name, update.lat, update.lng, 
		                    update.ratio, update.docked, update.empty, update.timestamp, 
		                    strategy.getType(), message, strategy.getColor())).iface());
	}
	
	action clearAlert(IAlertStrategy strategy) {
		strategy.pprint("Removing alert from strategy");
		dvHelper.remove(((new Alert).init(strategy.getCity(),strategy.getId(),strategy.getType())).iface());
	}

	action createUpperBoundary() {
		CreateUpperBoundaryAlert uba;
		on all CreateUpperBoundaryAlert():uba {
			DBKey key := getKey(uba.city, uba.id);
			strategies[key].append( (new UpperBoundary).init(uba.id, uba.city, uba.threshold, this) );
		}
	}

	action createLowerBoundary() {
		CreateLowerBoundaryAlert lba;
		on all CreateLowerBoundaryAlert():lba {
			DBKey key := getKey(lba.city, lba.id);
			strategies[key].append( (new LowerBoundary).init(lba.id, lba.city, lba.threshold, this) );
		}
	}
	
	action createRateThreshold() {
		CreateRateThresholdAlert rta;
		on all CreateRateThresholdAlert():rta {
			DBKey key := getKey(rta.city, rta.id);
			strategies[key].append( (new RateThreshold).init(rta.id, rta.city, rta.threshold, rta.duration, this) );
		}
	}
	
	action getKey(string city, integer id) returns DBKey {
		DBKey key := DBKey(city, id);
		if not strategies.hasKey(key) then {
			strategies.add(key, new sequence<IAlertStrategy> );
		}
		return key;
	}
}