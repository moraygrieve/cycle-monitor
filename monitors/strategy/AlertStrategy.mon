package com.jtech.alert;
using com.jtech.source.StationUpdate;

event AlertStrategy { 
	IAlertStrategy this;
	
	integer _id;
	string  _city;
	boolean _alerted;
	IAlertManager _manager;
	
    action init(integer id, string city, IAlertManager manager) returns IAlertStrategy {
        _id := id;
        _city := city;
        _manager := manager;
		
		this := new IAlertStrategy;
		this.getId := getId;
		this.getCity := getCity;
		this.getAlerted := getAlerted;
		this.check := check;
		this.alert := alert;
		this.clear := clear;
		return this;
	}
	
	//implement in extending strategy (abstract)
	action check(StationUpdate update) {}

	//base implementations
	action getId() returns integer { return _id; }
	action getCity() returns string { return _city; }
	action getAlerted() returns boolean { return _alerted; }
	action alert(StationUpdate update, string type, string message, string color) {
		_alerted := true;
		_manager.addAlert(update, type, message, color);
	}
	action clear(StationUpdate update, string type) {
		_alerted := false;
		_manager.clearAlert(update, type);
	}
}