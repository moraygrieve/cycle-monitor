package com.jtech.alert;
using com.jtech.source.StationUpdate;

event AlertStrategy { 
	IAlertStrategy this;
	integer _id;
	string  _city;
	string  _type;
	string  _color;
	boolean _alerted;
	IAlertManager _manager;
	
    action init(integer id, string city, string type, string color, IAlertManager manager) returns IAlertStrategy {
        _id := id;
        _city := city;
        _type := type;
        _color := color;
        _manager := manager;
        
        setupRemoveListener();
        
		this := new IAlertStrategy;
		this.check := check;
		return this;
	}
	
	//abstract implementations
	action check(StationUpdate update) {}

	//base implementations
	action getAlerted() returns boolean { return _alerted; }
	
	action alert(StationUpdate update, string message) {
		_alerted := true;
		_manager.addAlert(update, _type, message, _color);
	}
	
	action clear() {
		_alerted := false;
		_manager.clearAlert(_city, _id, _type);
	}
	
	action setupRemoveListener() {
		on all (RemoveAlerts() or RemoveAlert(_city,_id,_type) ){
    		if (_alerted) then { 
    			_alerted := false;
    			clear(); 
    		}
    	}	
	}
}