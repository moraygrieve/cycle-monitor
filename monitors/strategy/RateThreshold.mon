// Cycle Monitor, Copyright (C) 2015  M.B.Grieve
// This file is part of the Cycle Monitor example application.
//
// Cycle Monitor is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// Cycle Monitor is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with City Monitor.  If not, see <http://www.gnu.org/licenses/>.
//
// Contact: moray.grieve@me.com

package com.jtech.alert;
using com.apama.aggregates.first;
using com.apama.aggregates.last;
using com.jtech.source.StationUpdate;

event Result { float first; float last; }
event Data { string city; integer id; float ratio;  }

event RateThreshold {
	constant string  TYPE :="RATE_THRESHOLD";
	constant string  COLOR:="yellow";

	AlertStrategy base;
	float threshold;
	float duration;
	StationUpdate current;

	action init(integer id, string city, float threshold, float duration, IAlertManager manager) returns IAlertStrategy {    
		self.threshold := threshold;
		self.duration := duration;
		setupStreamListener();
		
		IAlertStrategy this := base.init(id, city, TYPE, COLOR, manager);
		return this;
	}

	//implementation of required actions
	action check(StationUpdate update) {
		current := update;
		route Data(base.city, base.id, update.ratio);
	}

	action setupStreamListener() {
		Result result;
		from u in all Data(city=base.city, id=base.id) within duration
		  select Result(first(u.ratio),last(u.ratio)):result {
			float change:=0.0;
			if ( not (result.first=result.last or result.first.isNaN() or result.last.isNaN()) ) then {
				change := ( (result.last-result.first)/(result.first) ).abs(); 
				checkForAlert(change);
			}
			log base.prefix+"Change="+change.toString()+",result="+result.toString()+",last="+current.toString() at DEBUG;
		}
	} 

	action checkForAlert(float change) {
		if (not base.alerted and change > threshold) then {
			base.alert(current, "Change of " +change.formatFixed(2) + " exceeds limit " + threshold.toString());
		}
		else if (base.alerted and change <= threshold) then {
			base.clear();
		}
	}
}
